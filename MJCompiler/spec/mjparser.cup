package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

// import java.io.*;
import rs.ac.bg.etf.pp1.ast.*;


parser code {:
	
	boolean errorDetected = false;
	
	Logger log = Logger.getLogger(getClass());
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }
    
    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }
    
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

// KEYWORDS
terminal PROG, PRINT, RETURN, CLASS, ENUM, ELSE, CONST, IF, DO, WHILE, NEW, READ, VOID, EXTENDS, CONTINUE, FOREACH, BREAK;

// OPERATORS
terminal PLUS, MINUS, MUL, DIV, MOD, EQ, NEQ, GRT, GRE, LESS, LEQ, AND, OR, ASSIGN, INC, DEC, SEMI, COLON, COMMA, DOT, LPAREN, RPAREN, LBRACKET, RBRACKET, LBRACE, RBRACE, LAMBDA;

// CHARACTERS AND BOOLEAN
terminal Character CHAR;
terminal Boolean BOOLEAN;

//NUMBERS AND INDENTIFICATORS
terminal Integer NUMBER;
terminal String IDENT;

// Program
nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program;

// Decl

nonterminal DecList;

nonterminal VarDecl, ConstDecl, ClassDecl;

	//ConstDecl
	nonterminal ConstDecList, ConstDecElement, ConstDecLastElement;

	//VarDecl
	nonterminal VarDecList, VarDecElement, VarDecLastElement;
	
		//Element
		nonterminal OptionalBrackets;
		
	//ClassDecl
	nonterminal Extended, ClassBody;
	nonterminal ListOfVarDecl, ConstructorDecList, ConstructorDecl;
	
	//Method
	nonterminal MethodDecl, MethodDecList, ListOfMethodDecl;
	nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodTypeName;
	
	//FormPairs
	nonterminal FormParsList, FormParsElement;
	
	


//Type

nonterminal rs.etf.pp1.symboltable.concepts.Struct Type;

//Statement
nonterminal ListOfStatements, Statement;

//LOOPS

nonterminal WhileFlag, TestForEach;

//Designator
nonterminal DesignatorStatement, ActPars, ListOfActPars, OptDesignatorList, ActParsFlag;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator, DesignatorOrEpsilon, ArrDsignator, ForeachIdent, ForeachFlag;

//Condition
nonterminal IfCond, Condition, CondTerm;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondFact;

//Expr
nonterminal rs.etf.pp1.symboltable.concepts.Struct Expr, Term, Factor, ActParsElement;


//Operators
nonterminal Relop, Mulop, Addop;

//IF ELSE HELP
nonterminal StartIf, EndIf, OrJump, StartIfStatement;

//---ListOf... - 0 or more  ...List - 1 or more


precedence left ELSE;


Program ::= (ProgramClass) PROG ProgName:program DecList LBRACE ListOfMethodDecl RBRACE;

ProgName ::= (ProgNameClass) IDENT:name;


DecList ::=	(VarDecListClass) DecList VarDecl  
		|	(ConstDecListClass) DecList ConstDecl  
		|	(ClassDecListClass) DecList ClassDecl 
		|	(NoDecListClass)/* epsilon */ 
		;
			

ConstDecl ::= (ConstDeclClass) CONST Type:varType ConstDecList;	

ConstDecList ::=	(ConstDecListElementClass) ConstDecElement ConstDecList
				|	(ConstDecLastElementClass) ConstDecLastElement		
				;

ConstDecElement ::=	(ConstElementBoolean) IDENT:constName ASSIGN BOOLEAN:value COMMA
				|	(ConstElementNumber) IDENT:constName ASSIGN NUMBER:value COMMA
				|	(ConstElementChar) IDENT:constName ASSIGN CHAR:value COMMA
				;
				
ConstDecLastElement ::=	(ConstElementBooleanLast) IDENT:constName ASSIGN BOOLEAN:value SEMI
					|	(ConstElementNumberLast) IDENT:constName ASSIGN NUMBER:value SEMI
					|	(ConstElementCharLast) IDENT:constName ASSIGN CHAR:value SEMI
					;

Type ::= (TypeClass) IDENT:name;

VarDecl ::= (VarDeclClass) Type:type VarDecList;


VarDecList ::=	(VarDecListElementClass) VarDecElement VarDecList
			|	(VarDecLastListElementClass) VarDecLastElement
			;
				
VarDecElement ::= 	(VarDecElementClass) IDENT:varName OptionalBrackets COMMA
				|	(VarDecElementErrClass) error COMMA:l
					{: parser.report_error("Izvrsen oporavak do , u liniji " + lleft, null); :}
				;

OptionalBrackets ::=	(ArrayBracketsClass) LBRACKET RBRACKET
					|	(NoArrayBracketsClass) /* epsilon */	
					; 
					

VarDecLastElement ::= 	(VarDecLastElementClass) IDENT:varName OptionalBrackets SEMI
					|	(VarDecLastElementErrClass) error SEMI:l
						{: parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null); :}
		    		;
			    
ClassDecl ::= 	(ClassDeclClass) CLASS IDENT:className Extended ClassBody;

Extended ::=	(ExtendedClass) EXTENDS Type:type LBRACE
			|	(NotExtendedClass) /*epsilon*/ LBRACE
			|	(ErrorExtendedClass) error LBRACE:l
				{: parser.report_error("Izvrsen oporavak do { u liniji " + lleft, null); :}
			;
			
ClassBody ::=	(ClassBodyVarClass) ListOfVarDecl RBRACE
			|	(ClassBodyVarEmptyClass) ListOfVarDecl LBRACE RBRACE RBRACE
			|	(ClassBodyVarMethodClass) ListOfVarDecl LBRACE MethodDecList RBRACE RBRACE
			|	(ClassBodyVarConstructorClass) ListOfVarDecl LBRACE ConstructorDecList RBRACE RBRACE
			|	(ClassBodyAllClass) ListOfVarDecl LBRACE ConstructorDecList MethodDecList RBRACE RBRACE
			;
			
ListOfVarDecl ::= (ListOfVarDeclClass) ListOfVarDecl VarDecl
				|	(EmptyListOfVarDeclClass) /*epsilon*/
				;
		
MethodDecList ::=	(MethodDecListClass) MethodDecList MethodDecl
				|	(SingleMethodDecListClass) MethodDecl
				;

ListOfMethodDecl ::=	(ListOfMethodDeclClass) ListOfMethodDecl MethodDecl
					|	(EmptyListOfMethodDeclClass) /*epsilon*/
					;

ConstructorDecList ::=	(ConstructorDecListClass) ConstructorDecList ConstructorDecl
					|	(SingleConstructorDecListClass)	ConstructorDecl	
					;
ConstructorDecl ::= (ConstructorDeclClass) IDENT:name LPAREN FormParsList ListOfVarDecl LBRACE ListOfStatements RBRACE;
			
MethodDecl ::= (MethodDeclClass) MethodTypeName LPAREN FormParsList ListOfVarDecl LBRACE ListOfStatements RBRACE;

MethodTypeName ::= 	(ReturnMethodClass) Type:retType IDENT:name
		   		|	(VoidMethodClass) VOID IDENT:name
		   		;

FormParsList ::=	(FormParsStartClass) FormParsElement COMMA FormParsList
				|	(FormParsNextElementClass) FormParsElement RPAREN
			 	|	(FormParsEnd) /* epsilon */ RPAREN //----mora ovde zbog opravka od greske
		     	|	(FormParsCommaErrorClass) error COMMA:l 
	    		 	{: parser.report_error("Izvrsen oporavak do , u liniji " + lleft, null); :} 
			    |
	    		 	(FormParsRparenErrorClass) error RPAREN:l
	    		 	{: parser.report_error("Izvrsen oporavak do ) u liniji " + lleft, null); :}
				;

FormParsElement ::=	(FormParsElement) Type:type IDENT:nameForm OptionalBrackets;

ListOfStatements ::= (ListOfStatementsClass) ListOfStatements Statement
					|	(EmptyListOfStatementsClass) /*epsilon*/
					;


Statement ::= 	(DesignatorStatementClass) DesignatorStatement
			|	(IfStatementClass) StartIf IF LPAREN IfCond StartIfStatement Statement EndIf
			|	(IfElseStatementClass) StartIf IF LPAREN IfCond StartIfStatement Statement EndIf ELSE Statement
			|	(WhileStatementClass) WhileFlag LPAREN Condition RPAREN Statement
			|	(BreakStatementClass) BREAK SEMI
			|	(ContinueStatementClass) CONTINUE SEMI
			|	(ReturnStatementClass) RETURN SEMI
			|	(ReturnExprStatementClass) RETURN Expr SEMI
			|	(ReadStatementClass) READ LPAREN Designator RPAREN SEMI
			|	(PrintStatementClass) PRINT LPAREN Expr RPAREN SEMI
			|	(PrintStatementWidthClass) PRINT LPAREN Expr COMMA NUMBER:width RPAREN SEMI
			|	(ForEachStatementClass) ForeachFlag ForeachIdent LAMBDA Statement RPAREN SEMI TestForEach
			|	(StatementBlock) LBRACE ListOfStatements RBRACE
			;
//Push vars stack
StartIf ::= (StartIf);

//Fixup 
EndIf ::= (EndIf);
StartIfStatement ::= (StartIfStatement);

WhileFlag ::= (WhileFlag) WHILE;

ForeachFlag ::= (ForeachFlag) Designator DOT FOREACH LPAREN;

ForeachIdent ::= (ForeachIdent) IDENT:feIdent;

TestForEach ::= (TestForEach);
	
IfCond ::= 	(IfCondClass) Condition RPAREN
		|	(ErrIfCondClass) error RPAREN:l 
			{: parser.report_error("Izvrsen oporavak do ) u liniji " + lleft, null); :}
		;

DesignatorStatement ::=	(DesignatorStatementAssignClass) Designator ASSIGN Expr SEMI
					|	(ErrDesignatorStatementAssignClass) error SEMI:l
						{: parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null); :}
					|	(DesignatorStatementFunctionClass) Designator LPAREN ActPars RPAREN SEMI
					|	(DesignatorStatementIncClass) Designator INC SEMI
					|	(DesignatorStatementDecClass) Designator DEC SEMI
					|	(DesignatorStatementArrayClass) LBRACKET OptDesignatorList RBRACKET ASSIGN Designator SEMI
					;
					
OptDesignatorList ::= 	(OptDesignatorListClass) OptDesignatorList COMMA DesignatorOrEpsilon
				   |	(SingleOrEndOfListDesignatorClass) DesignatorOrEpsilon
				   ;

DesignatorOrEpsilon ::=	(DesignArrAssignmentClass) Designator
					|	(DesignatorEpsilonClass) /* epsilon */
					;

Condition ::= 	(ConditionClass) Condition OrJump OR CondTerm
			|	(SingleConditionClass) CondTerm
			;
//If you have successfully reached end of term or and terms jump to if statement
OrJump ::= (OrJump);

CondTerm ::= 	(CondTermClass) CondTerm AND CondFact
			|	(SingleCondTermClass) CondFact
			;

CondFact ::=	(CondFactClass) Expr Relop Expr
			|	(SingleCondFactClass) Expr
			;

Expr ::=	(ExprClass) Expr Addop Term
		|	(PositiveExprClass) Term
		|	(NegativeExprClass) MINUS Term
		;
		
Term ::= 	(MultipleFactorsTermClass) Term Mulop Factor
		|	(SingleFactorTermClass) Factor
		;

Factor ::= 	(DesignatorFactorClass) Designator
		|	(FuncCallDesignatorClass) Designator LPAREN ActPars RPAREN
		|	(NumberFactorClass) NUMBER:NumValue
		|	(CharFactorClass) CHAR:CharValue
		|	(BoolFactorClass) BOOLEAN:BoolValue
		|	(ExpFactorClass) LPAREN Expr RPAREN
		|	(ArrayFactorClass) NEW Type LBRACKET Expr RBRACKET
		|	(ClassFactorClass) NEW Type LPAREN ActPars RPAREN
		;
	
Mulop ::=	(MulClass) MUL
		|	(DivClass) DIV
		|	(ModClass) MOD
		;	
ActPars ::= (ActualParsClass) ActParsFlag ListOfActPars
		| 	(NoActualParsClass) ActParsFlag /* epsilon */
		;

ActParsFlag ::= (ActParsFlag);

ListOfActPars ::= 	(ListOfActParExprClass) ListOfActPars COMMA ActParsElement
				|	(EmptyListOfActParClass) ActParsElement
				;
				
ActParsElement ::= (ActParsElement) Expr;
					
Addop ::= 	(PlusOpClass) PLUS
		|	(MinusOpClass) MINUS
		;
		
Designator ::=	(ClassDesignatorClass) Designator DOT IDENT:name
			|	(ArrayDesignatorClass) ArrDsignator LBRACKET Expr RBRACKET
			|	(DesignatorClass) IDENT:name 
			;

ArrDsignator ::= (ArrDesignator) Designator;

Relop ::= 	(EqOpClass) EQ
		| 	(NeqOpClass) NEQ
		| 	(GrOpClass) GRT
		| 	(GreOpClass) GRE
		| 	(LessOpClass) LESS
		| 	(LeqOpClass) LEQ
		;